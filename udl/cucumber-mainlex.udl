################################################################################
# UDL for Cucumber
################################################################################
language Cucumber

initial IN_SSL_DEFAULT

keywords [Feature Background 'Scenario Outline' Scenario Given When Then And But Examples '*']
keyword_style SSL_DEFAULT => SSL_WORD

pattern SECTIONKW  = '(Feature:|Background:|Scenario:|Scenario Outline:|Examples:)'
pattern SCENARIOKW = '(Given|When|Then|And|But|\*)'

state IN_SSL_DEFAULT:
'#'           : paint(upto, SSL_DEFAULT), => IN_SSL_COMMENT
'@'           : paint(upto, SSL_DEFAULT), => IN_SSL_TAG
'\''          : paint(upto, SSL_DEFAULT), => IN_SSL_SQSTRING
'"'           : paint(upto, SSL_DEFAULT), => IN_SSL_DQSTRING
/^\s*"""\s*$/ : paint(upto, SSL_DEFAULT), => IN_SSL_DOCSTRING
/[0-9]/       : paint(upto, SSL_DEFAULT), => IN_SSL_NUMBER
/^\s*\|/      : paint(upto, SSL_DEFAULT), => IN_SSL_TABLE
'<'           : paint(upto, SSL_DEFAULT), => IN_SSL_PLACEHOLDER
## Keywords won't hilite w/out these. Not sure why.
/^\s*$SECTIONKW\s+/    : paint(upto, SSL_DEFAULT), paint(include, SSL_WORD), => IN_SSL_DEFAULT
/^\s*$SCENARIOKW\s+/ : paint(upto, SSL_DEFAULT), paint(include, SSL_WORD), => IN_SSL_DEFAULT

## Comments
state IN_SSL_COMMENT:
/$/ : paint(upto, SSL_COMMENT), => IN_SSL_DEFAULT

## Tags
state IN_SSL_TAG:
/\s+/ : paint(upto, SSL_IDENTIFIER), => IN_SSL_DEFAULT

## Strings
state IN_SSL_SQSTRING:
'\'' : paint(include, SSL_STRING), => IN_SSL_DEFAULT
'<'  : paint(upto, SSL_STRING), spush_check(IN_SSL_SQSTRING), => IN_SSL_PLACEHOLDER

state IN_SSL_DQSTRING:
'"' : paint(include, SSL_STRING), => IN_SSL_DEFAULT
'<' : paint(upto, SSL_STRING), spush_check(IN_SSL_DQSTRING), => IN_SSL_PLACEHOLDER

state IN_SSL_DOCSTRING:
/^\s*"""\s*$/ : paint(include, SSL_STRING), => IN_SSL_DEFAULT
'<'           : paint(upto, SSL_STRING), spush_check(IN_SSL_DOCSTRING), \
                                         => IN_SSL_PLACEHOLDER

## Numbers
state IN_SSL_NUMBER:
/$/  : paint(upto, SSL_NUMBER), => IN_SSL_DEFAULT
'.'  : #stay
/\d/ : #stay
/./  : paint(upto, SSL_NUMBER), => IN_SSL_DEFAULT

## Placeholders
state IN_SSL_PLACEHOLDER:
'>' : paint(include, SSL_VARIABLE), spop_check, => IN_SSL_DEFAULT

## Data Tables
state IN_SSL_TABLE:
/\s*\|\s*$/ : paint(include, SSL_VARIABLE), => IN_SSL_DEFAULT
